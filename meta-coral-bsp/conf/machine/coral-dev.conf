#@TYPE: Machine
#@NAME: Google Coral Dev Board
#@SOC: i.MX8MQ
#@DESCRIPTION: Machine configuration for Google Coral Dev Board
#@MAINTAINER: Mirza Krak <mirza@mkrak.org>

MACHINEOVERRIDES =. "mx8:mx8m:mx8mq:"

WKS_FILE ?= "coral-bootpart.wks.in"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc

PREFERRED_PROVIDER_virtual/kernel ??= "linux-imx"

PREFERRED_PROVIDER_u-boot ??= "u-boot-coral"
PREFERRED_PROVIDER_virtual/bootloader ??= "u-boot-coral"

MACHINE_FEATURES += "wifi bluetooth optee qca6174"

MACHINE_EXTRA_RDEPENDS += "\
    kernel-modules \
    libedgetpu \
    firmware-qca6174 \
    kernel-module-qca6174 \
"

MACHINE_SOCARCH_FILTER_append_mx8mq = " \
    virtual/libopenvg \
    virtual/libgles1 \
    virtual/libgles2 \
    virtual/egl \
    virtual/mesa \
    virtual/libgl \
    virtual/libg2d \
"

KERNEL_DEVICETREE = "\
    freescale/fsl-imx8mq-phanbell.dtb \
    freescale/fsl-imx8mq-yorktown.dtb \
"

IMAGE_BOOTFILES += "\
    imx8mq_m4_TCM_hello_world.bin \
    imx8mq_m4_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mq_m4_TCM_rpmsg_lite_str_echo_rtos.bin \
"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "mx8mq_phanbell_defconfig,sdcard"

SPL_BINARY = "spl/u-boot-spl.bin"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = "\
    lpddr4_pmu_train_1d_imem.bin \
    lpddr4_pmu_train_1d_dmem.bin \
    lpddr4_pmu_train_2d_imem.bin \
    lpddr4_pmu_train_2d_dmem.bin \
"

# Set u-boot DTB
UBOOT_DTB_NAME = "fsl-imx8mq-phanbell.dtb"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "flash_evk flash_evk_no_hdmi"

# Set Serial console
SERIAL_CONSOLE = "115200 ttymxc0"

IMAGE_BOOTLOADER = "imx-boot"
BOOT_SPACE = "65536"
IMX_BOOT_SEEK = "33"

LOADADDR = ""

OPTEE_BIN_EXT = "8mq"

IMAGE_BOOT_FILES += "boot.scr"

# Use gstreamer 1.14 from meta-freescale
PREFERRED_VERSION_gstreamer1.0 ?= "1.14.imx"
PREFERRED_VERSION_gstreamer1.0-plugins-base ?= "1.14.imx"
PREFERRED_VERSION_gstreamer1.0-plugins-good ?= "1.14.imx"
PREFERRED_VERSION_gstreamer1.0-plugins-bad ?= "1.14.imx"
PREFERRED_VERSION_gstreamer1.0-libav ?= "1.14.0"

MACHINE_GSTREAMER_1_0_PLUGIN = "imx-gst1.0-plugin"
